# Maintainer: Jeroen Ooms <jeroenooms@gmail.com>

_realname=jags
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.3.0
pkgrel=9002
arch=('any')
pkgdesc="JAGS is Just Another Gibbs Sampler (mingw-w64)"
makedepends=("${MINGW_PACKAGE_PREFIX}-lapack" "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
groups=("rtools-${_realname}")
options=('strip' 'staticlibs')
license=('GPL')
url="http://mcmc-jags.sourceforge.net/"
source=("https://sourceforge.net/projects/mcmc-jags/files/JAGS/4.x/Source/JAGS-${pkgver}.tar.gz")
sha256sums=('8ac5dd57982bfd7d5f0ee384499d62f3e0bb35b5f1660feb368545f1186371fc')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../"JAGS-${pkgver}"/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
}
